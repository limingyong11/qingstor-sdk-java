plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.diffplug.gradle.spotless" version "3.27.2"
}

group = 'com.yunify'
version = '2.2.19'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'qingstor.sdk.java'  // this should be replaced near future.

            from components.java

            pom {
                name = project.name
                description = 'The official QingStor SDK for the Java programming language.'
                url = 'http://www.qingcloud.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yunify'
                        name = 'yunify'
                        email = 'sdk_group@yunify.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:yunify/qingstor-sdk-java.git'
                    developerConnection = 'scm:git:git@github.com:yunify/qingstor-sdk-java.git'
                    url = 'https://github.com/qingstor/qingstor-sdk-java'
                }
            }
        }
    }
    repositories {
        // local repo. For test usage.
        // maven {
        //     name = 'myRepo'
        //     url = "file://${buildDir}/repo"
        // }
        maven {
            name = 'mavencentral'
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv('SONATYPE_NEXUS_USERNAME')
                password System.getenv('SONATYPE_NEXUS_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

project.ext {
    cucumberVersion = '1.2.5'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

// task cucumber() {
//     dependsOn assemble, compileTestJava
//     doLast {
//         javaexec {
//             main = "io.cucumber.core.cli.Main"
//             // main = "cucumber.api.cli.Main"
//             classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
//             args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
//             // args = ['--plugin', 'pretty', '--glue', '.', 'tests/features']
//         }
//     }
// }

//create a single Jar with all dependencies
task buildTestJar(type: Jar) {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    archiveBaseName = project.name + '-test'
    from { configurations.cucumberRuntime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    implementation 'org.json:json:20140107'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'org.yaml:snakeyaml:1.26'
    implementation 'com.google.code.gson:gson:2.8.6'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20140107'
    testImplementation 'com.squareup.okhttp3:okhttp:3.11.0'
    testImplementation 'org.yaml:snakeyaml:1.26'
    testImplementation 'info.cukes:cucumber-java:' + cucumberVersion
    testImplementation 'info.cukes:cucumber-junit:' + cucumberVersion
}

spotless {
    java {
        // switch to AOSP style
        googleJavaFormat('1.7').aosp()
        // Add license to every java file.
        licenseHeaderFile rootProject.file('spotless.license.java')
    }
}
