plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.diffplug.gradle.spotless" version "3.27.2"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "io.freefair.lombok" version "5.0.0-rc6"
}

group = 'com.yunify'
version = '2.2.19'

repositories {
    mavenCentral()
}

java {
    jar {
        manifest {
            attributes('Implementation-Title': project.name,
                    'Implementation-Version': project.version)
        }
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    archiveClassifier = 'all-deps'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'qingstor.sdk.java'  // this should be replaced near future.

            from components.java

            artifact shadowJar

            pom {
                name = project.name
                description = 'The official QingStor SDK for the Java programming language.'
                url = 'http://www.qingcloud.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yunify'
                        name = 'yunify'
                        email = 'sdk_group@yunify.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:yunify/qingstor-sdk-java.git'
                    developerConnection = 'scm:git:git@github.com:yunify/qingstor-sdk-java.git'
                    url = 'https://github.com/qingstor/qingstor-sdk-java'
                }
            }
        }
    }
    repositories {
        // local repo. For test usage.
        // maven {
        //     name = 'myRepo'
        //     url = "file://${buildDir}/repo"
        // }
        maven {
            name = 'mavencentral'
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv('SONATYPE_NEXUS_USERNAME')
                password System.getenv('SONATYPE_NEXUS_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    implementation 'org.json:json:20140107'
    implementation 'com.squareup.okhttp3:okhttp:3.12.10'
    implementation 'org.yaml:snakeyaml:1.26'
    implementation 'com.google.code.gson:gson:2.8.6'
    // it's library's consumer's responsibility to choose a log implementation and set debug_level.
    implementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'io.cucumber:cucumber-java:5.5.0'
    testImplementation 'io.cucumber:cucumber-junit:5.5.0'
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'integration.cucumber', 'tests/features']
        }
    }
}

spotless {
    java {
        // switch to AOSP style
        googleJavaFormat('1.7').aosp()
        // Add license to every java file.
        licenseHeaderFile rootProject.file('spotless.license.java')
    }
}
